# dev & ubuntu-{seed,boot} partitions
setenv devtype  mmc
setenv devnum   1
setenv seedpart 1
setenv bootpart 2

# FIT Image & configuration
setenv kernel_name  kernel.img
setenv ramdisk_name initrd.img
setenv fit_config   rk3566-soquartz-cm4.dtb

echo "Loading ubuntu-seed configuration..."
setenv envaddr 0x00c08000
load ${devtype} ${devnum}:${seedpart} ${envaddr} uboot/ubuntu/boot.sel
env import ${envaddr}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${scriptaddr} uboot/ubuntu/boot.sel
    env import ${scriptaddr}
    setenv kernel_bootpart "${bootpart}"
    if test "${kernel_status}" = "try"
    then
        echo "Trying to boot with ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_try_kernel}"
    else
        echo "Using ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_kernel}"
    fi
else
    setenv prefix "systems/${snapd_recovery_system}/kernel"
    setenv kernel_bootpart "${seedpart}"
fi

setenv board_spec "earlycon=uart8250,mmio32,0xfe660000 console=ttyS2,1500000n8"
setenv bootargs   "${board_spec} rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system}"

echo "Loading kernel from ${devtype} ${devnum}:${kernel_bootpart} to address ${kernel_addr_r}"
load ${devtype} ${devnum}:${kernel_bootpart} ${kernel_addr_r} "${prefix}/${kernel_name}"

echo "Loading ramdisk from ${devtype} ${devnum}:${ramdisk_bootpart} to address ${ramdisk_addr_r}"
load ${devtype} ${devnum}:${kernel_bootpart} ${ramdisk_addr_r} "${prefix}/${ramdisk_name}"
setenv ramdisk_filesize ${filesize}

load ${devtype} ${devnum}:${kernel_bootpart} ${fdt_addr_r} "${prefix}/dtbs/rockchip/${fit_config}"

booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_filesize} ${fdt_addr_r}
